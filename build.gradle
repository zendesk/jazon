group 'com.zendesk.jazon'

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    maven {
        url baseMavenRepoAllUrl
        credentials {
            username baseMavenRepoAllUser
            password baseMavenRepoAllPassword
        }
    }
    jcenter()
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourceJar, javadocJar
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        jazon(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = 'Jazon'
                packaging = 'jar'
                description = 'A library for test assertions on JSON payloads.'
                url = 'https://github.com/zendesk/jazon'

                scm {
                    connection = 'scm:git:git://github.com/zendesk/jazon.git'
                    developerConnection = 'scm:git:ssh://github.com:zendesk/jazon.git'
                    url = 'http://github.com/zendesk/jazon/tree/master'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'pawel'
                        name = 'Paweł Mikołajczyk'
                        email = 'pmikolajczyk@zendesk.com'
                        organization = 'Zendesk'
                        organizationUrl = 'https://zendesk.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            String snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            String releaseUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            url = version.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'com.google.guava', name: 'guava', version: '21.0'

    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
